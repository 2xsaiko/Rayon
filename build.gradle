plugins {
	id 'com.github.johnrengelman.shadow' version "5.2.0"
	id 'fabric-loom' version '0.5-SNAPSHOT'
	id 'maven-publish'
}

subprojects {
	apply plugin: 'fabric-loom'
	archivesBaseName = rootProject.archives_base_name + "-" + project.name
	group = rootProject.maven_group + "." + rootProject.archives_base_name

	dependencies {
		compile rootProject.sourceSets.main.output
		modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	}
}

allprojects {
	apply plugin: 'com.github.johnrengelman.shadow'
	apply plugin: 'fabric-loom'

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	archivesBaseName = project.archives_base_name
	version = project.mod_version
	group = project.maven_group

	repositories {
		maven { url = "https://jitpack.io" }
	}

	dependencies {
		// Minecraft + Yarn
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

		// Fabric Loader + FAPI
		modCompile "net.fabricmc:fabric-loader:${project.loader_version}"
		modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

		// Transporter
		modApi "com.github.LazuriteMC:Transporter:${project.transporter_version}"
		include "com.github.LazuriteMC:Transporter:${project.transporter_version}"

		// Zip4j (shadowed)
		implementation "net.lingala.zip4j:zip4j:${project.zip4j_version}"
		shadow "net.lingala.zip4j:zip4j:${project.zip4j_version}"

		// LibBulletJME (shadowed)
		compile "com.github.stephengold:Libbulletjme:${project.libbulletjme_version}"
		shadow "com.github.stephengold:Libbulletjme:${project.libbulletjme_version}"
	}

	processResources {
		inputs.property "version", project.version

		from(sourceSets.main.resources.srcDirs) {
			include "fabric.mod.json"
			expand "version": project.version
		}

		from(sourceSets.main.resources.srcDirs) {
			exclude "fabric.mod.json"
		}
	}

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

shadowJar {
	configurations = [project.configurations.shadow]
}

jar {
	from "LICENSE"
}

remapJar {
	dependsOn shadowJar
	input.set shadowJar.archiveFile.get()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		mavenLocal()
	}
}